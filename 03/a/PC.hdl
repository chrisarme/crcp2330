// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:

	Inc16(in=outputOut, out=incOut);

	// Check if need to inc
	Mux16(a=outputOut, b=incOut, sel=inc, out=incCheck);

	// Check if need to load or pass incCheck
	Mux16(a=incCheck, b=in, sel=load, out=loadCheck);

	// Check if need to reset or pass loadCheck
	Mux16(a=loadCheck, b[0..15]=false, sel=reset, out=finalCheck);

	// Save/show data
	Register(in=finalCheck, load=true, out=out, out=outputOut);
}
